package mci

process main {
    products {
        - utility 1 one
    }

    products {
        - mass_used 1 kg
    }
    inputs {
        - mass_manufactured 1 kg
    }
}

process manufacture {
    parameters {
        - FR : 1.0 // fraction of recycled content
        - FU : 0 // fraction of reused content
        - FS : 0.0 //0.15 // fraction of biological feedstock
    }

    products {
        - mass_manufactured 1 kg
    }

    inputs {
        - recycled_feedstock ${FR} kg
    }

    resources {
        - virgin_feedstock ${1- FR - FU - FS} kg
        - ignored ${FS} kg
        - ignored ${FU} kg
    }

    emissions {
        - mass_transit 1 kg
    }
}

process collect {
    parameters {
        - CC : 0.0 // fraction for composting
        - I : 0.0 //1.0 // does bio material satisfy inclusion condition
        - EE : 0.75 // efficiency of the energy recovery process for material satisfying inclusion condition
        - BC : 0.45 // typical carbon content of bio material
        - CE : ${I * EE * BC} // fraction for energy recovery of material satisfying the inclusion condition
        - CR : 0.06 // fraction for recycling
        - CU : 0.0 // fraction for reuse
    }

    products {
        - material_for_recycling ${CR} kg
    }

    inputs {
        - mass_used 1 kg
    }

    emissions {
        - waste_W0 ${1 - CR - CU - CC - CE} kg
        - ignored ${CC} kg
        - ignored ${CE} kg
        - ignored ${CU} kg
    }

    emissions {
        - mass_transit 1 kg
    }
}

process recycling {
    parameters {
        - EC : 0.31 // efficiency of the recycling process
    }
    inputs {
        - material_for_recycling 1 kg
    }
    emissions {
        - ignored ${EC} kg
        - waste_WC ${1 - EC} kg
    }

    emissions {
        - mass_transit ${-0.5*(1 - EC)} kg
    }
}

process recycled_feedstock_production {
    parameters {
        - EF : 0.31 // efficiency of the recycled content production process
    }

    products {
        - recycled_feedstock ${EF} kg
    }

    resources {
        - ignored 1 kg
    }

    emissions {
        - waste_WF ${1 - EF} kg
    }

    emissions {
        - mass_transit ${0.5*(1 - EF)} kg
    }
}

process w0 {
    products {
        - waste_W0 1 kg
    }
    inputs {
        - unrecoverable_waste 1 kg
    }
}

process wf {
    products {
        - waste_WF 1 kg
    }
    inputs {
        - unrecoverable_waste 0.5 kg
    }
}

process wc {
    products {
        - waste_WC 1 kg
    }
    inputs {
        - unrecoverable_waste 0.5 kg
    }
}


process virgin_feedstock_lfi {
    products {
        - virgin_feedstock 1 kg
    }
    inputs {
        - mass_linear 1 kg
    }
}

process unrecoverable_waste_lfi {
    products {
        - unrecoverable_waste 1 kg
    }
    inputs {
        - mass_linear 1 kg
    }
}
