package v0_1_1.ssii


/*
    Process definitions
*/

process kleis {
    params {
        n_employees = 7 person
        surface = 150 m2
    }

    products {
        1 piece kleis
    }

    inputs {
        surface batiment
        n_employees * (200 day) * (3 piece/day) cafe
    }

    inputs {
        n_employees bureau from bureau(lifespan = 30 piece)
        n_employees chaise
        n_employees * (200 day) * (8 hour/day) ecran
        n_employees * (200 day) * (8 hour/day) laptop
        n_employees * (365 day) * (3 hour/day) natel
    }

    inputs {
        n_employees * (200 day) * (30 km/day) transport_mix
    }

    inputs {
        10 kWh chauffage
    }

    inputs {
        (200 day) * (24 hour/day) internet
    }
}


process bureau {
    params {
        lifespan = 15 piece
        weight = 30 kg
    }
    products {
        1 piece bureau
    }
    inputs {
        weight/lifespan furniture
    }
}

process chaise {
    params {
        lifespan = 15 piece
        weight = 30 kg
    }
    products {
        1 piece chaise
    }
    inputs {
        weight/lifespan furniture
    }
}

process laptop {
    products {
        1 hour laptop
    }
    inputs {
        1 hour operation_computer
    }
}

process natel {
    products {
        1 hour natel
    }
    inputs {
        1 hour consumer_electronics_smartphone
    }
}

process ecran {
    products {
        1 hour ecran
    }
    inputs {
        1 hour display_liquid_crystal
    }
}

process transport_mix {
    params {
        F_train = 80 percent
    }
    products {
        1 person*km transport_mix
    }

    inputs {
        F_train * (1 person*km) transport_passenger_train
        (100 percent - F_train) * (1 person*km) transport_regular_bus
    }
}


process internet {
    products {
        1 hour internet
    }

    inputs {
        1 hour internet_access
    }
}

process chauffage {
    products {
        1 kWh chauffage
    }

    inputs {
        1 kWh heat_district_industrial
    }
}

/*
    EI 39
*/

// PROXIES

process display_liquid_crystal {
    products {
        1 hour display_liquid_crystal
    }
    inputs {
        0.5 hour operation_computer
    }
}

process consumer_electronics_smartphone {
    products {
        1 hour consumer_electronics_smartphone
    }
    inputs {
        0.25 hour operation_computer
    }
}

// Emission factors

process operation_computer {
    products {
        1 hour operation_computer
    }

    emissions {
        0.060427 m3 water_to_turbine
        0.060426 m3 water
        5.843e-3 kg carbon_dioxide_fossil
        1.4e-5 kg methane_fossil
    }
}



process transport_passenger_train {
    products {
        1 person*km transport_passenger_train
    }
    emissions {
        9.921e-3 kg carbon_dioxide_fossil
    }
}

process transport_regular_bus {
    products {
        1 person*km transport_regular_bus
    }

    emissions {
        0.07889 kg carbon_dioxide_fossil
    }
}

process furniture {
    products {
        1 kg furniture
    }

    emissions {
        2.184 kg carbon_dioxide_fossil
        0.000155 m3 water_to_turbine
    }
}

process internet_access {
    products {
        1 hour internet_access
    }

    emissions {
        878.9e-6 kg carbon_dioxide_fossil
        950e-10 m3 water_to_turbine
    }
}

process heat_district_industrial {
    products {
        1 kWh heat_district_industrial
    }

    emissions {
        0.1149 kg carbon_dioxide_fossil
        1.1812e-6 m3 water_to_turbine
    }
}


