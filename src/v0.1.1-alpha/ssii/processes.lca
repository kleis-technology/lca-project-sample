package ssii

import prelude.units.*
import ecoinvent.*

/*
    Process definitions
*/

process p_kleis {
    params {
        n_employees = 7 piece
        surface = 150 m2
    }
    1 piece kleis

    inputs {
        surface batiment
        n_employees * (200 day) * (3 piece/day) cafe
    }

    inputs {
        n_employees bureau from p_bureau
        n_employees chaise from p_chaise
        n_employees * (200 day) * (8 hour/day) ecran from p_ecran
        n_employees * (200 day) * (8 hour/day) laptop from p_laptop
        n_employees * (365 day) * (3 hour/day) natel from p_natel
    }

    inputs {
        n_employees * (200 day) * (30 km/day) transport_mix from p_transport_mix
    }

    inputs {
        10 kWh chauffage from p_chauffage
    }

    inputs {
        (200 day) * (24 hour/day) internet from p_internet
    }
}


process p_bureau {
    params {
        lifespan = 15 piece
        weight = 30 kg
    }

    1 piece bureau
    inputs {
        weight/lifespan furniture from p_furniture
    }
}

process p_chaise {
    params {
        lifespan = 15 piece
        weight = 30 kg
    }

    1 piece chaise
    inputs {
        weight/lifespan furniture from p_furniture
    }
}

process p_laptop {
    1 hour laptop
    inputs {
        1 hour operation_computer from p_operation_computer
    }
}

process p_natel {
    1 hour natel
    inputs {
        1 hour consumer_electronics_smartphone from p_consumer_electronics_smartphone
    }
}

process p_ecran {
    1 hour ecran
    inputs {
        1 hour display_liquid_crystal from p_display_liquid_crystal
    }
}


process p_transport_mix {
    params {
        F_train = 80 percent
    }
    1 person*km transport_mix
    inputs {
        F_train * (1 person*km) transport_passenger_train from p_transport_passenger_train
        (100 percent - F_train) * (1 person*km) transport_regular_bus from p_transport_regular_bus
    }
}


process p_internet {
    1 hour internet

    inputs {
        1 hour internet_access from p_internet_access
    }
}

process p_chauffage {
    1 kWh chauffage

    inputs {
        1 kWh heat_district_industrial from p_heat_district_industrial
    }
}
